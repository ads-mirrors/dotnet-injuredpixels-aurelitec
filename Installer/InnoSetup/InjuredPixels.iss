; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "InjuredPixels"
#define MyAppVersion "4.0"
#define MyAppFileVersion "4.0.0.200"
#define MyAppPublisher "Aurelitec"
#define MyAppURL "http://www.aurelitec.com/injuredpixels/"
#define MyAppExeName "InjuredPixels.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{7AE8ACDB-C766-4639-9FEE-7062221B22F0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppPublisher}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=Files\License.txt
OutputBaseFilename=injuredpixels-install
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64 ia64
InternalCompressLevel=max
AppCopyright=Copyright (c) 2009-2017 Aurelitec
SetupIconFile=Assets\InjuredPixelsIcon.ico
UninstallDisplayIcon={app}\InjuredPixels.exe
VersionInfoVersion={#MyAppFileVersion}
UninstallDisplayName={#MyAppName}

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "Files\InjuredPixels.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\HomePage.link.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\InjuredPixels.exe.config"; DestDir: "{app}"; Flags: ignoreversion; AfterInstall: UpdateLogPath
Source: "Files\InjuredPixels.ini"; DestDir: "{userappdata}\Aurelitec\InjuredPixels"; Flags: ignoreversion onlyifdoesntexist
Source: "Files\License.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "Files\SharpConfig.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Dirs]
Name: "{commonappdata}\Aurelitec\InjuredPixels"

[Code]
procedure UpdateLogPath;
var
    AnsiFileData: AnsiString;
    UnicodeFileData: string;
begin
    LoadStringFromFile(ExpandConstant('{app}\InjuredPixels.exe.config'), AnsiFileData);
    UnicodeFileData := String(AnsiFileData);
    StringChange(UnicodeFileData, 'InjuredPixels.log', ExpandConstant('{commonappdata}\Aurelitec\InjuredPixels\InjuredPixels.log'));
    SaveStringToFile(ExpandConstant('{app}\InjuredPixels.exe.config'), AnsiString(UnicodeFileData), False);
end;
